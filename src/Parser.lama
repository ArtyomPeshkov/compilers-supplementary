-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun helperBinop (fst, op, snd) { 
  Binop (op, fst, snd)
}
-- Primary expression
var primary = memo $ eta (decimal @ fun (x) {Const (stringInt (x))} |
                          lident  @ fun (x) {Var (x)} |
                          inbr (s ("("), exp, s (")"))),
      exp = memo $ eta expr (
          {
            [Left, {[s ("!!"), helperBinop],
                    [s ("&&"), helperBinop]}],
            [Nona, {[s ("=="), helperBinop], 
                    [s ("!="), helperBinop], 
                    [s (">"), helperBinop], 
                    [s ("<"), helperBinop], 
                    [s (">="), helperBinop], 
                    [s ("<="), helperBinop]}],
            [Left, {[s ("+"), helperBinop], 
                    [s ("-"), helperBinop]}],
            [Left, {[s ("*"), helperBinop], 
                    [s ("/"), helperBinop], 
                    [s ("%"), helperBinop]}]
          }, primary);

var stmtElse = memo $ eta syntax (
  kFi {Skip} |
  kElse s=stmt kFi {s} |
  kElif e=exp kThen s=stmt o=stmtElse {If (e, s, o)}
);

var stmtSimp = memo $ eta syntax (
  kRead x=inbr[s("("), lident, s(")")] { Read (x) } |
  kWrite x=inbr[s("("), exp, s(")")] { Write (x) } |
  kSkip { Skip } |
  kIf e=exp kThen s=stmt o=stmtElse {If (e, s, o)} |
  kWhile e=exp kDo s=stmt kOd {While (e, s)} |
  kDo s=stmt kWhile e=exp kOd {DoWhile (s, e)} |
  kFor i=stmt s[","] e=exp s[","] s=stmt kDo o=stmt kOd {Seq (i, While (e, Seq (o, s)))} |
  x=lident s[":="] y=exp { Assn (x, y) }
);

var stmt = memo $ eta syntax (
  stmtSimp | f=stmtSimp s[";"] l=stmt { Seq (f, l) }
);
   

-- Public top-level parser
public parse = stmt;
